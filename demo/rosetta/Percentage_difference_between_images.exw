--
-- demo\rosetta\Percentage_difference_between_images.exw
-- =====================================================
--
-- Author Pete Lomax, October 2018
--
--  Requires read_ppm() from [[Bitmap/Read_a_PPM_file#Phix|Read_a_PPM_file]], write_ppm() from [[Bitmap/Write_a_PPM_file#Phix|Write_a_PPM_file]]. 
--  Uses Lenna50.ppm and Lenna100.ppm, which you will have to find/download to demo/rosetta yourself.
free_console()
--with trace

include ppm.e

function split_colour(integer c)
    return sq_div(sq_and_bits(c, {#FF0000,#FF00,#FF}),
                                 {#010000,#0100,#01})
end function

function percentage_diff(sequence img1, img2)
    if length(img1)!=length(img2)
    or length(img1[1])!=length(img2[1]) then
        return "sizes do not match"
    end if
    atom diff = 0
    for i=1 to length(img1) do
        for j=1 to length(img1[i]) do
            integer {r1,g1,b1} = split_colour(img1[i,j]),
                    {r2,g2,b2} = split_colour(img2[i,j])
            diff += abs(r1-r2)+abs(g1-g2)+abs(b1-b2)
        end for 
    end for 
    return 100*diff/(length(img1)*length(img1[1]))/3/255
end function

--trace(1)
sequence img1 = read_ppm("Lenna50.ppm"),
         img2 = read_ppm("Lenna100.ppm")
?percentage_diff(img1,img2)

{} = wait_key()




