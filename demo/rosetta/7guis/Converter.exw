--
-- demo\rosetta\7guis\Converter.exw
-- ================================
--
include pGUI.e
 
Ihandle dlg, Tc, Tf
 
function ctof_cb(Ihandle /*Tc*/)
sequence p = scanf(IupGetAttribute(Tc,"VALUE"),"%f")
    if length(p)=1 then
        atom f = p[1][1]*(9/5)+32
        IupSetStrAttribute(Tf,"VALUE","%g",{f})
    end if
    return IUP_CONTINUE
end function

function ftoc_cb(Ihandle /*Tf*/)
sequence p = scanf(IupGetAttribute(Tf,"VALUE"),"%f")
    if length(p)=1 then
        atom c = (p[1][1]-32)*(5/9)
        IupSetStrAttribute(Tc,"VALUE","%g",{c})
    end if
    return IUP_CONTINUE
end function

IupOpen()
Tc = IupText("VALUECHANGED_CB",Icallback("ctof_cb"))
Tf = IupText("VALUECHANGED_CB",Icallback("ftoc_cb"))
dlg = IupDialog(IupHbox({Tc,IupLabel("Celsius ="),
                         Tf,IupLabel("Fahrenheit")},
                        "MARGIN=20x10,GAP=10,ALIGNMENT=ACENTER"),
                `TITLE="Temperature Converter"`)
IupShow(dlg)
IupMainLoop()
IupClose()

