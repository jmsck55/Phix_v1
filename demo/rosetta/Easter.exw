--
-- demo\rosetta\Easter.exw
-- =======================
--
-- The Phix timedate routines make no attempt to support pre-1752 dates, but the algorithm seems to work.
function easter(integer year)
-- from https://en.wikipedia.org/wiki/Computus#Anonymous_Gregorian_algorithm
    integer a = mod(year,19),
            b = floor(year/100),
            c = mod(year,100),
            d = floor(b/4),
            e = mod(b,4),
            f = floor((b+8)/25),
            g = floor((b-f+1)/3),
            h = mod(19*a+b-d-g+15,30),
            i = floor(c/4),
            k = mod(c,4),
            l = mod(32+2*e+2*i-h-k,7),
            m = floor((a+11*h+22*l)/451),
            n = h+l-7*m+114,
            month = floor(n/31),
            day = mod(n,31)+1
    return {year,month,day,0,0,0,0,0,0}
end function

constant dates = {{"Easter   ",0},
                  {"Ascension",39},
                  {"Pentecost",49},
                  {"Trinity  ",56},
                  {"Corpus   ",60}}

constant fmt = join(repeat(" %12s",length(dates))),
         fmt0 = "   "&fmt&"\n",
         fmtN = "%4d"&fmt&"\n"

--with trace
include builtins\timedate.e

set_timedate_formats({"Ddd ddth Mmm"})

procedure show(integer year)
    if year=0 then
        printf(1,fmt0, columnize(dates,1)[1])
    else
        timedate e = easter(year)
        sequence args = {year}
        for i=1 to length(dates) do
--trace(1)
            string d = format_timedate(adjust_timedate(e,timedelta(days:=dates[i][2])))
            args = append(args,d)
        end for
        printf(1,fmtN, args)
    end if
end procedure
show(0)
for year=400 to 2100 by 100 do
    show(year)
end for
show(0)
for year=2010 to 2020 do
    show(year)
end for

{} = wait_key()

--/*
       Easter        Ascension     Pentecost     Trinity       Corpus
 400 Sun 02nd Apr  Thu 11th May  Sun 21st May  Sun 28th May  Thu 01st Jun
 500 Sun 04th Apr  Thu 13th May  Sun 23rd May  Sun 30th May  Thu 03rd Jun
 600 Sun 13th Apr  Thu 22nd May  Sun 01st Jun  Sun 08th Jun  Thu 12th Jun
 700 Sun 15th Apr  Thu 24th May  Sun 03rd Jun  Sun 10th Jun  Thu 14th Jun
 800 Sun 23rd Apr  Thu 01st Jun  Sun 11th Jun  Sun 18th Jun  Thu 22nd Jun
 900 Sun 28th Mar  Thu 06th May  Sun 16th May  Sun 23rd May  Thu 27th May
1000 Sun 30th Mar  Thu 08th May  Sun 18th May  Sun 25th May  Thu 29th May
1100 Sun 08th Apr  Thu 17th May  Sun 27th May  Sun 03rd Jun  Thu 07th Jun
1200 Sun 09th Apr  Thu 18th May  Sun 28th May  Sun 04th Jun  Thu 08th Jun
1300 Sun 18th Apr  Thu 27th May  Sun 06th Jun  Sun 13th Jun  Thu 17th Jun
1400 Sun 20th Apr  Thu 29th May  Sun 08th Jun  Sun 15th Jun  Thu 19th Jun
1500 Sun 01st Apr  Thu 10th May  Sun 20th May  Sun 27th May  Thu 31st May
1600 Sun 02nd Apr  Thu 11th May  Sun 21st May  Sun 28th May  Thu 01st Jun
1700 Sun 11th Apr  Thu 20th May  Sun 30th May  Sun 06th Jun  Thu 10th Jun
1800 Sun 13th Apr  Thu 22nd May  Sun 01st Jun  Sun 08th Jun  Thu 12th Jun
1900 Sun 15th Apr  Thu 24th May  Sun 03rd Jun  Sun 10th Jun  Thu 14th Jun
2000 Sun 23rd Apr  Thu 01st Jun  Sun 11th Jun  Sun 18th Jun  Thu 22nd Jun
2100 Sun 28th Mar  Thu 06th May  Sun 16th May  Sun 23rd May  Thu 27th May
       Easter        Ascension     Pentecost     Trinity       Corpus
2010 Sun 04th Apr  Thu 13th May  Sun 23rd May  Sun 30th May  Thu 03rd Jun
2011 Sun 24th Apr  Thu 02nd Jun  Sun 12th Jun  Sun 19th Jun  Thu 23rd Jun
2012 Sun 08th Apr  Thu 17th May  Sun 27th May  Sun 03rd Jun  Thu 07th Jun
2013 Sun 31st Mar  Thu 09th May  Sun 19th May  Sun 26th May  Thu 30th May
2014 Sun 20th Apr  Thu 29th May  Sun 08th Jun  Sun 15th Jun  Thu 19th Jun
2015 Sun 05th Apr  Thu 14th May  Sun 24th May  Sun 31st May  Thu 04th Jun
2016 Sun 27th Mar  Thu 05th May  Sun 15th May  Sun 22nd May  Thu 26th May
2017 Sun 16th Apr  Thu 25th May  Sun 04th Jun  Sun 11th Jun  Thu 15th Jun
2018 Sun 01st Apr  Thu 10th May  Sun 20th May  Sun 27th May  Thu 31st May
2019 Sun 21st Apr  Thu 30th May  Sun 09th Jun  Sun 16th Jun  Thu 20th Jun
2020 Sun 12th Apr  Thu 21st May  Sun 31st May  Sun 07th Jun  Thu 11th Jun
--*/
