--
-- demo\rosetta\9billionnames.exw
-- ==============================
--

sequence cache = {{1}}
function cumu(integer n)
sequence r
    for l=length(cache) to n do
        r = {0}
        for x=1 to l do
            r = append(r,r[-1]+cache[l-x+1][min(x,l-x)+1])
        end for
        cache = append(cache,r)
    end for
    return cache[n]
end function

function row(integer n)
sequence r = cumu(n+1)
sequence res = repeat(0,n)
    for i=1 to n do
        res[i] = r[i+1]-r[i]
    end for
    return res
end function

for i=1 to 25 do
    puts(1,repeat(' ',50-2*i))
    sequence r = row(i)
    for j=1 to i do
        printf(1,"%4d",r[j])
    end for
    puts(1,"\n")
end for
--====--

--{{trans|C}}
--{{libheader|mpfr}}
include mpfr.e
 
sequence p
 
procedure calc(integer n)
    n += 1
    for k=1 to n-1 do
        integer d = n - k * (3 * k - 1) / 2;
        if d<1 then exit end if
        if and_bits(k,1) then mpz_add(p[n],p[n],p[d])
                         else mpz_sub(p[n],p[n],p[d]) end if
        d -= k;
        if d<1 then exit end if
        if and_bits(k,1) then mpz_add(p[n],p[n],p[d])
                         else mpz_sub(p[n],p[n],p[d]) end if
    end for
end procedure
 
constant cx = {23, 123, 1234, 12345}
--constant cx = {1,2,3,5,10,21,22,23, 123, 1234}
puts(1,"sums:\n")
integer at = 1
atom t0 = time()
p = mpz_inits(cx[$]+1)
mpz_set_si(p[1],1)
for i=1 to cx[$] do
    calc(i)
    if i=cx[at] then
        printf(1,"%2d:%s\n",{i,mpz_get_str(p[i+1])})
        at += 1
    end if
end for
?elapsed(time()-t0)


--A simple plot, using row() from above:
include pGUI.e
--#withtype Ihandle
--#withtype Ihandles

Ihandle dlg, plot

IupOpen()
IupControlsOpen()
plot = IupPlot("MENUITEMPROPERTIES=Yes, SIZE=640x320")
IupSetAttribute(plot, "TITLE", "9 Billion Names");
IupSetAttribute(plot, "TITLEFONTSIZE", "10");
IupSetAttribute(plot, "TITLEFONTSTYLE", "ITALIC");
IupSetAttribute(plot, "GRIDLINESTYLE", "DOTTED");
IupSetAttribute(plot, "GRID", "YES");
IupSetAttribute(plot, "AXS_XLABEL", "x");
IupSetAttribute(plot, "AXS_YLABEL", "G(x)");
IupSetAttribute(plot, "AXS_XFONTSTYLE", "ITALIC");
IupSetAttribute(plot, "AXS_YFONTSTYLE", "ITALIC");
IupSetAttribute(plot, "AXS_XSCALE", "LOG10");
IupSetAttribute(plot, "AXS_YSCALE", "LOG10");
IupSetAttribute(plot, "AXS_YTICKSIZEAUTO", "NO");
IupSetAttribute(plot, "AXS_YTICKMAJORSIZE", "8");
IupSetAttribute(plot, "AXS_YTICKMINORSIZE", "0");
IupPlotBegin(plot)
for x=1 to 999 do
    IupPlotAdd(plot, x, sum(row(x)))
end for
{} = IupPlotEnd(plot)
dlg = IupDialog(plot)
IupSetAttribute(dlg, "TITLE", "9 Billion Names")
IupMap(dlg)
IupShowXY(dlg,IUP_CENTER,IUP_CENTER)
IupMainLoop()
IupClose()

--{} = wait_key()

--/*

rows:
 1: 1
 2: 1,1
 3: 1,1,1
 4: 1,2,1,1
 5: 1,2,2,1,1
 6: 1,3,3,2,1,1
 7: 1,3,4,3,2,1,1
 8: 1,4,5,5,3,2,1,1
 9: 1,4,7,6,5,3,2,1,1
10: 1,5,8,9,7,5,3,2,1,1
11: 1,5,10,11,10,7,5,3,2,1,1
12: 1,6,12,15,13,11,7,5,3,2,1,1
13: 1,6,14,18,18,14,11,7,5,3,2,1,1
14: 1,7,16,23,23,20,15,11,7,5,3,2,1,1
15: 1,7,19,27,30,26,21,15,11,7,5,3,2,1,1
16: 1,8,21,34,37,35,28,22,15,11,7,5,3,2,1,1
17: 1,8,24,39,47,44,38,29,22,15,11,7,5,3,2,1,1
18: 1,9,27,47,57,58,49,40,30,22,15,11,7,5,3,2,1,1
19: 1,9,30,54,70,71,65,52,41,30,22,15,11,7,5,3,2,1,1
20: 1,10,33,64,84,90,82,70,54,42,30,22,15,11,7,5,3,2,1,1
21: 1,10,37,72,101,110,105,89,73,55,42,30,22,15,11,7,5,3,2,1,1
22: 1,11,40,84,119,136,131,116,94,75,56,42,30,22,15,11,7,5,3,2,1,1
23: 1,11,44,94,141,163,164,146,123,97,76,56,42,30,22,15,11,7,5,3,2,1,1
24: 1,12,48,108,164,199,201,186,157,128,99,77,56,42,30,22,15,11,7,5,3,2,1,1
25: 1,12,52,120,192,235,248,230,201,164,131,100,77,56,42,30,22,15,11,7,5,3,2,1,1
sums:
23:1255
123:2552338241
1234:156978797223733228787865722354959930
12345:69420357953926116819562977205209384460667673094671463620270321700806074195845953959951425306140971942519870679768681736

--*/
